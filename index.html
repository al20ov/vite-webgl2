<!DOCTYPE html>
<html lang="en">

<head>
  <meta charset="UTF-8" />
  <link rel="icon" type="image/svg+xml" href="favicon.svg" />
  <meta name="viewport" content="width=device-width, initial-scale=1.0" />
  <title>Vite App</title>
</head>

<body>
  <canvas id="canvas">
  </canvas>
  <script type="shader/vertex" id="vertex-shader">
    #version 300 es

    layout(location=0) in vec3 position;
    layout(location=1) in vec3 normal;

    uniform mat4 model;
    uniform mat4 view;
    uniform mat4 projection;

    //out vec2 vUV;
    out vec3 vNormal;
    out vec3 worldPos;

    void main() {
      worldPos = vec3(model * vec4(position, 1.0));
      vNormal = mat3(model) * normal;

      gl_Position = projection * view * vec4(worldPos, 1.0);
    }
  </script>
  <script type="shader/fragment" id="fragment-shader">
    #version 300 es
    precision highp float;

    #define NBR_LIGHTS 1
    const float PI = 3.14159265359;

    out vec4 fragColor;
    in vec3 worldPos;
    in vec3 vNormal;

    uniform vec3 camPos;

    uniform vec3 albedo;
    uniform float metallic;
    uniform float roughness;
    uniform float ao;

    // lights
    uniform vec3 lightPositions[NBR_LIGHTS];

    vec3 fresnelSchlick(float cosTheta, vec3 F0) {
      return max(F0 + (1.0 - F0) * pow(1.0 - min(cosTheta,1.0), 5.0), 0.0);
    }

    float DistributionGGX(vec3 N, vec3 H, float roughness) {
      float a      = roughness*roughness;
      float a2     = a*a;
      float NdotH  = max(dot(N, H), 0.0);
      float NdotH2 = NdotH*NdotH;

      float denom = (NdotH2 * (a2 - 1.0) + 1.0);
      denom = PI * denom * denom;

      return a2 / max(denom, 0.0000001);
    }

    float GeometrySchlickGGX(float NdotV, float roughness) {
      float r = (roughness + 1.0);
      float k = (r*r) / 8.0;

      float num   = NdotV;
      float denom = NdotV * (1.0 - k) + k;

      return num / denom;
    }
    float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness) {
      float NdotV = max(dot(N, V), 0.000001);
      float NdotL = max(dot(N, L), 0.000001);
      float ggx2  = GeometrySchlickGGX(NdotV, roughness);
      float ggx1  = GeometrySchlickGGX(NdotL, roughness);

      return ggx1 * ggx2;
    }

    void main() {
      vec3 N = normalize(vNormal);
      vec3 V = normalize(camPos -worldPos);

      vec3 F0 = vec3(0.04);
      F0 = mix(F0, albedo, metallic);

      vec3 Lo = vec3(0.0);
      for (int i = 0; i < NBR_LIGHTS; ++i) {
        vec3 L = normalize(lightPositions[i] - worldPos);
        vec3 H = normalize(V + L);
        float distance = length(lightPositions[i] - worldPos);
        float attenuation = 1.0 / (distance * distance);
        vec3 radiance = vec3(400.0) * attenuation;

        float NDF = DistributionGGX(N, H, roughness);
        float G   = GeometrySmith(N, V, L, roughness);
        vec3 F    = fresnelSchlick(max(dot(H, V), 0.0), F0);

        vec3 kS = F;
        vec3 kD = vec3(1.0) - kS;
        kD *= 1.0 - metallic;

        //vec3 numerator    = NDF * G * F;
        //float denominator = 4.0 * max(dot(N, V), 0.0) * max(dot(N, L), 0.0)  + 0.0001;
        //vec3 specular     = numerator / denominator;
        vec3 specular = NDF * G * F;


        float NdotL = max(dot(N, L), 0.0);
        Lo += (kD * albedo / PI + specular) * radiance * NdotL;
      }
      vec3 ambient = vec3(0.03) * albedo * ao;
      vec3 color = ambient + Lo;

      color = color / (color + vec3(1.0));
      color = pow(color, vec3(1.0/2.2));

      fragColor = vec4(color, 1.0);
    }
  </script>
  <script type="module" src="/main.js"></script>
</body>

</html>